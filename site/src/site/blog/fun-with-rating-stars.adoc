= Fun with rating stars
Paul King
:revdate: 2023-01-25T00:00:00+00:00
:keywords: groovy
:description: Inspired by a recent tweet, this blog \
looks at producing a string of stars corresponding to a rating.

== Star Ratings

A recent tweet showed some C# code for producing a String of stars that might
be used when displaying ratings on a website:

image:img/star_ratings_csharp.png[Image of original tweet,366]

Let's have a look at several ways to do the same thing in Groovy.

[source]
----
def rating0(percentage) {
    int stars = Math.ceil(percentage * 10)
    ("ğŸ”µ" * stars).padRight(10, "âšª")
}
----

[source]
----
def rating1(percentage) {
    int stars = Math.ceil(percentage * 10)
    "ğŸ”µ" * stars + "âšª" * (10-stars)
}
----

[source]
----
def rating2(percentage) {
    int skip = 10 - Math.ceil(percentage * 10)
    "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšªâšªâšªâšªâšªâšªâšªâšª"[skip..<10+skip]
}
----

[source]
----
def rating3(percentage) {
    switch(percentage) {
        case 0             -> "âšªâšªâšªâšªâšªâšªâšªâšªâšªâšª"
        case { it <= 0.1 } -> "ğŸ”µâšªâšªâšªâšªâšªâšªâšªâšªâšª"
        case { it <= 0.2 } -> "ğŸ”µğŸ”µâšªâšªâšªâšªâšªâšªâšªâšª"
        case { it <= 0.3 } -> "ğŸ”µğŸ”µğŸ”µâšªâšªâšªâšªâšªâšªâšª"
        case { it <= 0.4 } -> "ğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšªâšªâšªâšª"
        case { it <= 0.5 } -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšªâšªâšª"
        case { it <= 0.6 } -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšªâšª"
        case { it <= 0.7 } -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšª"
        case { it <= 0.8 } -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšª"
        case { it <= 0.9 } -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšª"
        default            -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µ"
    }
}
----

If you want you can test the various edge cases:

[source]
----
for (i in 0..3)
    for (j in [0, 0.09, 0.1, 0.11, 0.9, 1])
        println "rating$i"(j)

----

== Increasing Robustness

The code examples here assume that the input is in the range `0 \<= percentage \<= 1`. There are several tweaks we could do to guard against inputs outside those ranges.

We could simply add an assert, e.g.:

[source]
----
def rating0b(percentage) {
    assert percentage >= 0 && percentage <= 1
    int stars = Math.ceil(percentage * 10)
    ("ğŸ”µ" * stars).padRight(10, "âšª")
}
----

Or, if we wanted to not fail, tweak some of our conditions, e.g.&nbsp;for
`rating3`, instead of `case 0`, we could use `case { it <= 0 }`.

We could push the checks into our types by making a special class, `Percent` say, which only permitted the allowed values:

[source]
----
final class Percent {
    final Double value
    Percent(Double value) {
        assert value >= 0 && value <= 1
        this.value = value
    }
}
----
And we could optionally also use some metaprogramming to provide a custom `isCase` method:

[source]
----
Double.metaClass.isCase = { Percent p -> delegate >= p.value }
----
Which means we could tweak the rating method to be:

[source]
----
def rating3b(Percent p) {
    switch(p) {
        case 0.0d -> "âšªâšªâšªâšªâšªâšªâšªâšªâšªâšª"
        case 0.1d -> "ğŸ”µâšªâšªâšªâšªâšªâšªâšªâšªâšª"
        case 0.2d -> "ğŸ”µğŸ”µâšªâšªâšªâšªâšªâšªâšªâšª"
        case 0.3d -> "ğŸ”µğŸ”µğŸ”µâšªâšªâšªâšªâšªâšªâšª"
        case 0.4d -> "ğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšªâšªâšªâšª"
        case 0.5d -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšªâšªâšª"
        case 0.6d -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšªâšª"
        case 0.7d -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšªâšª"
        case 0.8d -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšªâšª"
        case 0.9d -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µâšª"
        default   -> "ğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µğŸ”µ"
    }
}
----

We can be fancier here using `@EqualsAndHashCode` on the `Percent` class and/or using `@Delegate` on the `value` property, depending on how rich in
functionality we wanted the `Percent` instances to be.

Alternatively, we could use a design-by-contract approach:

[source]
----
@Requires({ percentage >= 0 && percentage <= 1 })
def rating1b(percentage) {
    int stars = Math.ceil(percentage * 10)
    "ğŸ”µ" * stars + "âšª" * (10-stars)
}
----

== References

https://twitter.com/JeroenFrijters/status/1615204074588180481[Original tweet]
